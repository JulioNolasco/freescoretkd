#! /usr/bin/perl

use lib qw( /usr/local/freescore/lib );
use Data::Dumper;
use Try::Tiny;
use Mojolicious::Lite;
use YAML;
use FreeScore::Forms::WorldClass;
use JSON::XS;

our $json = new JSON::XS();

# ============================================================
# JUDGE INTERFACE
# ============================================================

# ------------------------------------------------------------
# GET THE NUMBER OF JUDGES FOR THIS DIVISION
# ------------------------------------------------------------
get '/:tournament/:ring/judges' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $progress   = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division   = $progress->current();

	try {
		send_response( $self, { 'description' => 'Number of Judges', 'division' => $division->{ name }, 'judges' => $division->{ judges } } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ------------------------------------------------------------
# NEXT ROUND OR DIVISION
# ------------------------------------------------------------
get( '/:tournament/:ring/:object/:goto' => [ 
	object => qr/(?:division|round|athlete|form)/, 
	goto   => qr/(?:next|previous|\d+)/, 
])->to( cb => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $object     = $self->param( 'object' );
	my $goto       = $self->param( 'goto' );
	my $progress   = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division   = $progress->current();

	try {
		if      ( $goto eq 'next' ) {
			if    ( $object eq 'division' ) { $progress->next();             
			                                  $division = $progress->current(); 
			                                  $division->navigate_to_start();
			                                }
			elsif ( $object eq 'round'    ) { $division->next_round();       }
			elsif ( $object eq 'athlete'  ) { $division->next_athlete();     } 
			elsif ( $object eq 'form'     ) { $division->next_form();        }

		} elsif ( $goto eq 'previous' ) {
			if    ( $object eq 'division' ) { $progress->previous();         
			                                  $division = $progress->current(); 
			                                  $division->navigate_to_start();
			                                } 
			elsif ( $object eq 'round'    ) { $division->previous_round();   }
			elsif ( $object eq 'athlete'  ) { $division->previous_athlete(); }
			elsif ( $object eq 'form'     ) { $division->previous_form();    }

		} elsif ( $goto =~ /\d+/ ) {
			if    ( $object eq 'division' ) { $progress->navigate( $goto ); }
			else                            { $division->navigate( $object, $goto ); }
			
		}
		$division->write();
		$progress->write();
		send_response( $self, { 'description' => 'Navigation', 'division' => $division->{ name }, 'athlete' => $division->{ current }, 'state' => $division->{ state }, 'round' => $division->{ round }, 'form' => $division->{ form } });

	} catch {
		send_response( $self, { error => $_ });
	}
});

# ------------------------------------------------------------
# TOGGLE CURRENT DIVISION STATE BETWEEN DISPLAY AND SCORE
# ------------------------------------------------------------
get '/:tournament/:ring/display' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $progress   = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division   = $progress->current();

	if( $division->is_display() ) { $division->score();   } 
	else                          { $division->display(); }

	try {
		$division->write();
		$progress->write();
		send_response( $self, { 'description' => 'Update Display State', 'state' => $division->{ state } } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ------------------------------------------------------------
# WRITE JUDGE SCORE FOR ATHLETE
# ------------------------------------------------------------
get '/:tournament/:ring/:judge/:major/:minor/:rhythm/:power/:ki' => sub { 
	my $self         = shift;
	my $tournament   = $self->param( 'tournament' );
	my $ring         = $self->param( 'ring'       );
	my $judge        = $self->param( 'judge'      );
	my $major        = get_score( $self, 'major'  );
	my $minor        = get_score( $self, 'minor'  );
	my $rhythm       = get_score( $self, 'rhythm' );
	my $power        = get_score( $self, 'power'  );
	my $ki           = get_score( $self, 'ki'     );
	my $progress     = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division     = $progress->current();
	my $athlete      = $division->{ athletes }[ $division->{ current } ];
	my $score        = { major => $major, minor => $minor, rhythm => $rhythm, power => $power, ki => $ki };

	try {
		$division->record_score( $judge, $score );
		$division->write();
		$progress->write();
		$division->update_status();
		send_response( $self, { description => 'Scoring athlete', athlete => $athlete->{ name }, round => $division->{ round }, judge => ($judge + 1), score => $score } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ============================================================
# COORDINATOR INTERFACE
# ============================================================

# ------------------------------------------------------------
# FASTMATCH TRAINING DIVISION
# ------------------------------------------------------------
post '/:tournament/:ring/fastmatch' => sub { 
	my $self        = shift;
	my $tournament  = $self->param( 'tournament' );
	my $ring        = $self->param( 'ring' );
	my $i           = $self->param( 'div_index' );
	my $j           = $self->param( 'ath_index' );
	my $name        = $self->param( 'name' );
	my $progress    = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $content     = $self->req->body;

	try {
		my $archive = "archive.20" . join( "-", reverse map { sprintf "%02d", ($_ % 100) } ((localtime())[ 0 .. 5 ])) . ".tar.gz";
		`cd $progress->{ path } && tar -czf $archive *.txt && rm -f *.txt && touch div.wc01.txt`;
		$progress->{ current } = 0;
		$progress->write();
		my $division = new FreeScore::Forms::WorldClass::Division( $progress->{ path }, 'wc01', $ring );
		my $header = $json->decode( $content );
		foreach my $key (keys %$header) {
			next if $key eq 'athletes';
			$division->{ $key } = $header->{ $key };
		}
		foreach my $athlete (@{ $header->{ athletes }}) {
			push @{ $division->{ athletes }}, { name => $athlete };
			print STDERR "Adding $athlete\n";
		}
		$division->write();
		$division = new FreeScore::Forms::WorldClass::Division( $progress->{ path }, 'wc01', $ring );
		$division->write();
		send_response( $self, { description => "FastMatch division created" } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ------------------------------------------------------------
# ADD NEW ATHLETE OR UPDATE ATHLETE'S NAME
# ------------------------------------------------------------
get '/:tournament/:ring/:div_index/:ath_index/name/:name' => sub { 
	my $self        = shift;
	my $tournament  = $self->param( 'tournament' );
	my $ring        = $self->param( 'ring' );
	my $i           = $self->param( 'div_index' );
	my $j           = $self->param( 'ath_index' );
	my $name        = $self->param( 'name' );
	my $progress    = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division    = $progress->{ divisions }[ $i ];
	my $description = undef;

	if( $j > $#{ $division->{ athletes }} ) { 
		push @{ $division->{ athletes }}, { name => $name }; 
		$description = "Added athlete $name.";
	} else {
		my $athlete        = $division->{ athletes }[ $j ];
		$description = "Changed athlete name from '$athlete->{ name }' to '$name'.";
		$athlete->{ name } = $name;
	}

	try {
		$division->write();
		send_response( $self, { description => $description } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ------------------------------------------------------------
# POST THE DIVISION HEADER DATA FOR WRITING
# ------------------------------------------------------------
post '/:tournament/:ring/:div_index/text' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $i          = $self->param( 'div_index' );
	my $progress   = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division   = $progress->{ divisions }[ $i ];
	my $content    = $self->req->body;

	try {
		my $header = $json->decode( $content );
		foreach my $key (keys %$header) {
			$division->{ $key } = $header->{ $key };
		}
		$division->write();
		send_response( $self, { description => "Successfully wrote header to $division->{ id }" } );

	} catch {
		send_response( $self, { error => $_ });
	}
};


# ------------------------------------------------------------
# GET THE DIVISION TEXT DATA
# ------------------------------------------------------------
get '/:tournament/:ring/:div_index/text' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $i          = $self->param( 'div_index' );
	my $progress   = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division   = $progress->{ divisions }[ $i ];

	try {
		my $content = '';
		open FILE, $division->{ file } or die "Database Error: Can't read '$division->{ file }' $!";
		while( <FILE> ) { $content .= $_; }
		close FILE;
		send_response( $self, { content => $content } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ------------------------------------------------------------
# POST THE DIVISION TEXT DATA FOR WRITING
# ------------------------------------------------------------
post '/:tournament/:ring/:div_index/text' => sub { 
	my $self       = shift;
	my $tournament = $self->param( 'tournament' );
	my $ring       = $self->param( 'ring' );
	my $i          = $self->param( 'div_index' );
	my $progress   = new FreeScore::Forms::WorldClass( $tournament, $ring );
	my $division   = $progress->{ divisions }[ $i ];
	my $content    = $self->req->body;

	try {
		open FILE, ">$division->{ file }" or die "Database Error: Can't write to '$division->{ file }' $!";
		print FILE $content;
		close FILE;
		send_response( $self, { description => "Successfully wrote to $division->{ file }" } );

	} catch {
		send_response( $self, { error => $_ });
	}
};

# ============================================================
# ERROR TRAPPING
# ============================================================
any '*command' => sub {
	my $self = shift;
	my $command = $self->param( 'command' );
	send_response( $self, { error => "Forms::WorldClass: Unknown command: $command" });
};

# ============================================================
# HYPNOTOAD SERVER
# ============================================================
app->config( hypnotoad => { listen => [ 'http://*:3088' ], pid_file => '/var/run/worldclass.pid' });
app->start();

# ============================================================
sub get_score {
# ============================================================
	my $ajax  = shift;
	my $name  = shift;

	my $score = $ajax->param( $name );
	if( not defined $score || $score eq '' ) { $score = -1.0; }
	else                                     { $score /= 10;  }
	return sprintf( "%.1f", $score );
}

# ============================================================
sub send_response {
# ============================================================
	my $self = shift;
	my $data = shift;
	$self->res->headers->header( 'Access-Control-Allow-Origin' => 'http://freescore.net' );
	$self->render( json => $data );
};
